      PROGRAM MAIN
C  Speckle masking main program.
C
C  Usage:
C  ======
C  sm file_obs fpixels_obs lpixels_obs radius_obs
C    file_ref radius_ref
C    fit_method max_width prefix
C
C  Declaration:
C  ============
      IMPLICIT NONE
      INCLUDE 'fftw3.f'
      INTEGER :: FP(3),LP(3),MW,BUFFERSIZE,M,N,NBISP,K,L
      INTEGER*8 :: PLAN
      DOUBLE PRECISION :: DROBS,DRREF,DSNR,DTMP
      DOUBLE PRECISION, ALLOCATABLE :: DAVG(:,:),DBETA(:),DPHI(:,:)
     &  ,DRHO(:,:),DPSDOBS(:,:),DPSDREF(:,:),DSM(:,:)
      DOUBLE COMPLEX :: ZTMP
      DOUBLE COMPLEX, ALLOCATABLE :: ZBISPOBS(:),ZBISPREF(:),
     & ZIN(:,:),ZOUT(:,:)
      CHARACTER(LEN=256) :: FILEOBS,ARG,FILEREF,PREFIX
      CHARACTER(LEN=10) :: FTMETHOD
C
C  Statements:
C  ===========
C
      CALL GETARG(1,FILEOBS)
      CALL GETARG(2,ARG)
      READ(ARG,*) FP(1),FP(2),FP(3)
      CALL GETARG(3,ARG)
      READ(ARG,*) LP(1),LP(2),LP(3)
      CALL GETARG(4,ARG)
      READ(ARG,*) DROBS
      CALL GETARG(5,FILEREF)
      CALL GETARG(6,ARG)
      READ(ARG,*) DRREF
      CALL GETARG(7,FTMETHOD)
      CALL GETARG(8,ARG)
      READ(ARG,*) MW
      CALL GETARG(9,PREFIX)
C  The padding size must be even:
      M=LP(1)-FP(1)+1
      N=LP(2)-FP(2)+1
      NBISP=(MW+1)*(2*N-MW)*M*(M+2)/8
      PRINT *,'Bispectrum size (MB): ',DBLE(NBISP)*16/1024/1024
      ALLOCATE(DAVG(M,N))
      CALL AVERAGE(FILEOBS,FP,LP,DAVG,10)
      CALL GETSNR(M,N,DROBS,DSNR,DAVG,FTMETHOD)
      ALLOCATE(ZBISPOBS(NBISP))
      CALL BISPECTRUM(FILEOBS,FP,LP,DAVG,DROBS,FTMETHOD,
     &  MW,ZBISPOBS)
      CALL AVERAGE(FILEREF,FP,LP,DAVG,10)
      ALLOCATE(ZBISPREF(NBISP))
      CALL BISPECTRUM(FILEREF,FP,LP,DAVG,DRREF,FTMETHOD,
     &  MW,ZBISPREF)
      ALLOCATE(DBETA(NBISP))
      DO K=1,NBISP
        IF (ZABS(ZBISPREF(K))>DBLE(1.0)/DSNR)THEN
          ZTMP=ZBISPOBS(K)/ZBISPREF(K)
          DBETA(K)=DATAN2(DIMAG(ZTMP),DREAL(ZTMP))
        ELSE
          DBETA(K)=0
        END IF
      END DO
      DEALLOCATE(ZBISPOBS)
      DEALLOCATE(ZBISPREF)
      ALLOCATE(DPHI(M,N))
      CALL PHASERECURSION(M,N,MW,DBETA,DPHI)
      DEALLOCATE(DBETA)
      ALLOCATE(DPSDOBS(M,N))
      ALLOCATE(DPSDREF(M,N))
      CALL GETPSD(FILEOBS,FP,LP,DROBS,FTMETHOD,DPSDOBS)
      CALL GETPSD(FILEREF,FP,LP,DRREF,FTMETHOD,DPSDREF)
      ALLOCATE(DRHO(M,N))
      DRHO=DPSDOBS/DPSDREF
      ALLOCATE(ZIN(M,N))
      ALLOCATE(ZOUT(M,N))
      CALL DFFTW_PLAN_DFT_2D(PLAN,M,N,ZIN,ZOUT,1,
     &  FFTW_ESTIMATE+FFTW_DESTROY_INPUT)
      ZIN=CMPLX(DRHO*DCOS(DPHI),DRHO*DSIN(DPHI))
      CALL DFFTW_EXECUTE_DFT(PLAN,ZIN,ZOUT)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_RHO.FITS',(/1,1,1/),(/M,N,1/),
     &  DRHO)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_PHI.FITS',(/1,1,1/),(/M,N,1/),
     &  DPHI)
      ALLOCATE(DSM(M,N))
      DSM=DREAL(ZOUT)
      CALL DFFTSHIFT(M,N,DSM)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_SMK.FITS',(/1,1,1/),(/M,N,1/),
     &  DSM)
      DEALLOCATE(DSM)
      DEALLOCATE(DPHI)
      DEALLOCATE(ZIN)
      DEALLOCATE(ZOUT)
      DEALLOCATE(DRHO)
      DEALLOCATE(DPSDOBS)
      DEALLOCATE(DPSDREF)
      CALL DFFTW_DESTROY_PLAN(PLAN)
      STOP
      END PROGRAM MAIN