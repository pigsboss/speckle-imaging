      PROGRAM MAIN
C  Speckle masking main program.
C
C  Usage:
C  ======
C  sm file_obs fpixels_obs lpixels_obs radius_obs
C    file_ref radius_ref
C    fit_method max_width prefix
C
C  Declaration:
C  ============
      IMPLICIT NONE
      INCLUDE 'fftw3.f'
      INTEGER :: FP(3),LP(3),MW,BUFFERSIZE,M,N,LBISP,K,L
      INTEGER*8 :: PLAN
      DOUBLE PRECISION :: DROBS,DRREF,DSNR,DTMP
      DOUBLE PRECISION, ALLOCATABLE :: DAVG(:,:),DBETA(:),DPHI(:,:)
     &  ,DRHO(:,:),DPSDOBS(:,:),DPSDREF(:,:),DSM(:,:),DB(:,:),DBG(:,:)
      DOUBLE COMPLEX :: ZTMP
      DOUBLE COMPLEX, ALLOCATABLE :: ZBISPOBS(:),ZBISPREF(:),
     & ZIN(:,:),ZOUT(:,:)
      CHARACTER(LEN=256) :: FILEOBS,ARG,FILEREF,PREFIX
      CHARACTER(LEN=10) :: FTMETHOD
C
C  Statements:
C  ===========
C
      CALL GETARG(1,FILEOBS)
      CALL GETARG(2,ARG)
      READ(ARG,*) FP(1),FP(2),FP(3)
      CALL GETARG(3,ARG)
      READ(ARG,*) LP(1),LP(2),LP(3)
      CALL GETARG(4,ARG)
      READ(ARG,*) DROBS
      CALL GETARG(5,FILEREF)
      CALL GETARG(6,ARG)
      READ(ARG,*) DRREF
      CALL GETARG(7,FTMETHOD)
      CALL GETARG(8,ARG)
      READ(ARG,*) MW
      CALL GETARG(9,PREFIX)
      M=LP(1)-FP(1)+1
      N=LP(2)-FP(2)+1
      LBISP=(MW+1)*(2*N-MW)*M*(M+2)/8
      PRINT *,'Bispectrum size (MB): ',DBLE(LBISP)*16/1024/1024
      ALLOCATE(DAVG(M,N))
      ALLOCATE(DBG(M,N))
      ALLOCATE(DB(M,N))
      CALL AVERAGE(FILEOBS,FP,LP,DAVG,10)
      CALL GETSNR(M,N,DROBS,DSNR,DAVG,FTMETHOD)
      CALL BGFIT2P0(M,N,DROBS,DAVG,DBG,DB(1,1))
      DBG=DBG*DBLE(1.01)
      ALLOCATE(ZBISPOBS(LBISP))
      CALL BISPECTRUM(FILEOBS,FP,LP,MW,DBG,ZBISPOBS)
      CALL AVERAGE(FILEREF,FP,LP,DAVG,10)
      CALL BGFIT2P0(M,N,DRREF,DAVG,DBG,DB(1,1))
      DBG=DBG*DBLE(1.01)
      ALLOCATE(ZBISPREF(LBISP))
      CALL BISPECTRUM(FILEREF,FP,LP,MW,DBG,ZBISPREF)
      ALLOCATE(DBETA(LBISP))
      DO K=1,LBISP
        IF (ZABS(ZBISPREF(K)).GT.DBLE(0))THEN
          ZTMP=ZBISPOBS(K)/ZBISPREF(K)
        ELSE
          ZTMP=ZBISPOBS(K)/ZBISPREF(K)
          DBETA(K)=0
        END IF
        DBETA(K)=DATAN2(DIMAG(ZTMP),DREAL(ZTMP))
      END DO
      DEALLOCATE(ZBISPOBS)
      DEALLOCATE(ZBISPREF)
      ALLOCATE(DPHI(M,N))
      CALL PHASERECURSION(M,N,MW,DBETA,DPHI)
      DEALLOCATE(DBETA)
      ALLOCATE(DPSDOBS(M,N))
      ALLOCATE(DPSDREF(M,N))
      ALLOCATE(DSM(M,N))
      ALLOCATE(ZIN(M,N))
      ALLOCATE(ZOUT(M,N))
      CALL GETPSD(FILEOBS,FP,LP,DROBS,FTMETHOD,DPSDOBS)
      CALL GETPSD(FILEREF,FP,LP,DRREF,FTMETHOD,DPSDREF)
      CALL DFFTSHIFT(M,N,DPSDOBS)
      CALL DFFTSHIFT(M,N,DPSDREF)
      CALL DFFTW_PLAN_DFT_2D(PLAN,M,N,ZIN,ZOUT,1,
     &  FFTW_ESTIMATE+FFTW_DESTROY_INPUT)
      ZIN=CMPLX(DSQRT(DPSDOBS)*DCOS(DPHI),DSQRT(DPSDOBS)*DSIN(DPHI))
      CALL DFFTW_EXECUTE_DFT(PLAN,ZIN,ZOUT)
      DSM=DREAL(ZOUT)
      CALL DFFTSHIFT(M,N,DSM)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_REC.FITS',(/1,1,1/),(/M,N,1/),
     &  DSM)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_MODOBS.FITS',(/1,1,1/),(/M,N,1/),
     &  DSQRT(DPSDOBS))
      CALL WRITEIMAGE(TRIM(PREFIX)//'_MODREF.FITS',(/1,1,1/),(/M,N,1/),
     &  DSQRT(DPSDREF))
      ALLOCATE(DRHO(M,N))
      DO K=1,M
        DO L=1,N
          IF (DABS(DPSDREF(K,L)).GT.0)THEN
            DRHO(K,L)=DSQRT(DPSDOBS(K,L)/DPSDREF(K,L))
          ELSE
            DRHO(K,L)=DSQRT(DPSDOBS(K,L))
          END IF
        END DO
      END DO
      ZIN=CMPLX(DRHO*DCOS(DPHI),DRHO*DSIN(DPHI))
      CALL DFFTW_EXECUTE_DFT(PLAN,ZIN,ZOUT)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_MOD.FITS',(/1,1,1/),(/M,N,1/),
     &  DRHO)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_PHASE.FITS',(/1,1,1/),(/M,N,1/),
     &  DPHI)
      DSM=DREAL(ZOUT)
      CALL DFFTSHIFT(M,N,DSM)
      CALL WRITEIMAGE(TRIM(PREFIX)//'_RECDEMOD.FITS',(/1,1,1/),
     &  (/M,N,1/),DSM)
      DEALLOCATE(DSM)
      DEALLOCATE(DPHI)
      DEALLOCATE(ZIN)
      DEALLOCATE(ZOUT)
      DEALLOCATE(DRHO)
      DEALLOCATE(DPSDOBS)
      DEALLOCATE(DPSDREF)
      CALL DFFTW_DESTROY_PLAN(PLAN)
      STOP
      END PROGRAM MAIN